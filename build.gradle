import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'application'
    id "org.beryx.runtime" version "1.13.1"
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id 'maven-publish'
    id 'idea'
}

group 'org.doddle-owl'
version = '24.12.1'
mainClassName = 'io.github.doddle_owl.DODDLE_OWL'
sourceCompatibility = targetCompatibility = '17'

project.ext {
    artifactId = 'doddle-owl'
    defaultEncoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

wrapper {
    gradleVersion = '7.3'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperty 'file.encoding', 'UTF-8'
}

tasks.withType(JavaExec) {
    systemProperty 'file.encoding', 'UTF-8'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Main-Class': 'io.github.doddle_owl.DODDLE_OWL'
    }
}

idea {
    module {
        inheritOutputDirs = true
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ['java.desktop', 'java.logging', 'java.xml', 'java.sql']
    imageDir = file("$buildDir/doddle-owl-${version}")
    imageZip = file("$buildDir/doddle-owl-${version}.zip")

    jpackage {
        outputDir = 'my-packaging'
        mainJar = "doddle-owl-${version}.jar"
        imageName = "doddle-owl-${version}"
        skipInstaller = false
        installerName = "doddle-owl"
        if (OperatingSystem.current().isWindows()) {
            jpackageHome = 'C:/Program Files/Java/jdk-17.0.1'
            installerType = 'msi'
            imageOptions = ['--verbose', '--icon', './src/main/resources/images/doddle-owl.ico']
            installerOptions = ['--vendor', 'Takeshi Morita', '--win-shortcut']
        } else if (OperatingSystem.current().isMacOsX()) {
            jpackageHome = '/Library/Java/JavaVirtualMachines/jdk-17.jdk/Contents/Home'
            installerType = 'dmg'
            imageOptions = ['--verbose', '--icon', './src/main/resources/images/doddle-owl.icns']
            installerOptions = ['--vendor', 'Takeshi Morita']
        }
    }
}

tasks.runtime.doLast {
    copy {
        from('./README.md')
        into("$buildDir/doddle-owl-${version}")
    }
    copy {
        from('./README.ja.md')
        into("$buildDir/doddle-owl-${version}")
    }
    copy {
        from('./LICENSE')
        into("$buildDir/doddle-owl-${version}")
    }
    copy {
        from('./src/main/resources/jpwn_dict_1.1/')
        into("$buildDir/doddle-owl-${version}/ontologies/jpwn_dict_1.1/")
    }
}

shadowJar {
    mergeServiceFiles()
    manifest {
        attributes 'Main-Class': 'io.github.doddle_owl.DODDLE_OWL'
    }
    archiveFileName = "doddle-owl-${version}.jar"
    exclude 'jpwn_dict_1.1/'
}

dependencies {
    implementation 'com.formdev:flatlaf:3.5.4'
    implementation 'org.xerial:sqlite-jdbc:3.47.1.0'
    implementation 'commons-cli:commons-cli:1.9.0'
    implementation 'org.apache.pdfbox:pdfbox:2.0.32'
    implementation 'net.sf.extjwnl:extjwnl:2.0.5'
    implementation 'net.sf.extjwnl:extjwnl-data-wn31:1.2'
    implementation 'com.atilika.kuromoji:kuromoji-ipadic:0.9.0'
    implementation 'org.apache.poi:poi-ooxml:3.17'
    implementation 'edu.stanford.nlp:stanford-parser:3.9.2'
    implementation 'org.apache.jena:apache-jena-libs:5.2.0'
    implementation 'org.slf4j:slf4j-simple:1.7.36'
    implementation 'log4j:log4j:1.2.17'
    implementation 'com.optimaize.languagedetector:language-detector:0.6'
    compileOnly 'org.projectlombok:lombok:1.18.36'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.3'
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId = 'io.github.doddle-owl'
            artifactId = 'doddle-owl'

            from components.java
            pom {
                name = 'DODDLE-OWL'
                description = 'a Domain Ontology rapiD DeveLopment Environment - OWL extension'
                url = 'http://doddle-owl.github.io/'
                licenses {
                    license {
                        name = 'GNU GENERAL PUBLIC LICENSE, Version 3'
                        url = 'http://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 't-morita'
                        name = 'Takeshi Morita'
                        email = 'morita@it.aoyama.ac.jp'
                    }
                }
            }
        }
    }
}